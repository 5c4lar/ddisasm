//===- arch_x86_32.dl ---------------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
#include "arch_x86.dl"

.comp X86_32 : X86 {

#include "registers_x86_32.dl"

pointer_size(4).

inferred_main_in_reg(Push,Reg):-
    inferred_main_dispatch(Call),
    // Get the value pushed onto the stack as an argument (assumes cdecl).
    next(Push,Call),
    instruction_get_operation(Push,"PUSH"),
    instruction_get_src_op(Push,_,Op),
    (
        op_indirect_contains_reg(Op,Reg);
        op_regdirect_contains_reg(Op,Reg)
    ).

.override inferred_main_dispatch

// ELF X86-32 may have an additional function call in _start, so we add an
// additional heuristic that checks for a consecutive HLT instruction.
inferred_main_dispatch(CallEA):-
    do_infer_main_function(),
    CallEA = min EA:{
        start_function(Entry),
        instruction(EA,_,_,CallOp,_,_,_,_,_,_),
        arch.call_operation(CallOp),
        code(EA),
        next(EA,HltEA),
        instruction(HltEA,_,_,"HLT",_,_,_,_,_,_),
        EA >= Entry
     }.

// call __x86.get_pc_thunk.REG
pc_relative_addr(EA,Reg,PC):-
    get_pc_thunk(EA_thunk,Reg),
    direct_call(EA,EA_thunk),
    instruction(EA,Size,_,_,_,_,_,_,_,_),
    PC = as(EA+Size,number).

// pop PC
pc_relative_addr(EA,Reg,PC):-
    pc_load_call(_,EA),
    instruction_get_op(EA,_,Op),
    op_regdirect(Op,Reg),
    PC = as(EA,number).
}

abi_intrinsic(EA,Name):-
    get_pc_thunk(EA,_),
    symbol(EA,_,_,_,_,_,_,_,Name).

abi_intrinsic(EA,"__dso_handle"):-
    got_relative_operand(_,_,EA),
    symbol(EA,_,_,_,_,_,_,_,"__dso_handle").

// Identify load PC operations:
// e.g. (clang)
//       call   .L1$pb
// .L1$pb:
//        pop    REG
// .Ltmp0:
//        add    REG, offset _GLOBAL_OFFSET_TAB LE_+(.Ltmp0-.L1$pb)
// e.g. (gcc)
//   call _x86.get_pc_thunk.REG
//   add REG, _GLOBAL_OFFSET_TABLE_
got_reference_pointer(as(Offset,address)),
reg_has_got(EA,Reg):-
    value_reg(EA,Reg,_,"NONE",_,Offset,_),
    got_section(Name),
    section(Name,_,as(Offset,address)).

reg_has_got(EA_load,Reg_load):-
    // Store GOT pointer
    reg_has_got(EA,Reg_store),
    def_used(EA,Reg_store,EA_store,_),
    reg_stored_in_stack(EA_store,Reg_store,StackPos,StackFrame),
    // Load GOT pointer
    (
        arch.frame_pointer(Reg_base);
        arch.stack_pointer(Reg_base)
    ),
    reg_loaded_from_stack(EA_load,Reg_load,Reg_base,StackPos,StackFrame).

inferred_special_symbol(EA,"_GLOBAL_OFFSET_TABLE_","LOCAL","NONE","Beg"):-
    !symbol(_,_,_,_,_,_,_,_,"_GLOBAL_OFFSET_TABLE_"),
    got_reference_pointer(EA).

// GCC GOT-relative offset variant:
//     add     eax, OFFSET FLAT:_GLOBAL_OFFSET_TABLE_
symbolic_expr(EA+ImmediateOffset,4,"_GLOBAL_OFFSET_TABLE_",0):-
    reg_has_got(EA,Reg),
    got_reference_pointer(Got),
    op_immediate_and_reg(EA,"ADD",Reg,Index,Value),
    instruction_immediate_offset(EA,Index,ImmediateOffset,_),
    Got = EA+as(Value,address).

// CLANG GOT-relative offset variant:
//     add     ebx, offset _GLOBAL_OFFSET_TABLE_+(.Ltmp0-.L1$pb)
symbolic_operand_attribute(EA,Index,"GotOff"),
symbol_minus_symbol(EA+ImmediateOffset,4,Ref,EA,1,0):-
    reg_has_got(EA,Reg),
    got_reference_pointer(Got),
    op_immediate_and_reg(EA,"ADD",Reg,Index,Value),
    instruction_immediate_offset(EA,Index,ImmediateOffset,_),
    def_used(Ref,_,EA,_),
    Got = Ref+as(Value,address).

symbolic_operand_attribute(EA,Index,"PltRef"):-
    (
      direct_jump(EA,_);
      direct_call(EA,_)
    ),
    symbolic_operand(EA,Index,Dest,_),
    plt_block(Dest,_).

symbolic_operand_attribute(EA,Index,"GotOff"):-
    got_relative_operand(EA,Index,_).

symbolic_operand_attribute(EA,Index,"GotRef"):-
    got_relative_operand(EA,Index,Dest),
    got_reference(Dest,_).

symbolic_operand_attribute(EA,Index,"NtpOff"):-
    got_relative_operand(EA,Index,Dest),
    got_reference(Dest,_),
    relocation(Dest,"TLS_TPOFF",_,_,_,_,_).

plt_entry(EA,Function):-
    plt_section(SecName),
    section(SecName,Size,Beg),
    EA < Beg+Size,
    EA >= Beg,
    indirect_jump(EA),
    instruction_get_op(EA,_,Op),
    (
      //  jmp [EBX+Offset]
      op_indirect(Op,"NONE",_,"NONE",1,Offset,_),
      got_section(Name),
      section(Name,_,Got),
      Got_entry=Got+as(Offset,address)
      ;
      // jmp [Offset]
      op_indirect(Op,"NONE","NONE","NONE",1,Offset,_),
      Got_entry=as(Offset,address)
    ),
    relocation(Got_entry,_,Function,_,_,_,_).

// Segment-based references to thread-local storage,
//   e.g.   mov    eax, DWORD PTR gs:i@ntpoff
//     or   mov    eax, DWORD PTR gs:0xfffffff0
symbolic_operand_candidate(EA,Index,as(Reference+Offset,address),"data"):-
    instruction_get_op(EA,Index,Op),
    op_indirect(Op,"GS","NONE","NONE",_,Offset,_), Offset < 0,
    tls_segment(_,TlsEnd,Align),
    Reference = as(TlsEnd,number) + as(TlsEnd % max(Align,1),number).

//   e.g.  mov  eax, qword ptr gs:[0]
//         lea  eax, [eax + threadLocal@NTPOFF]
symbolic_operand_candidate(EA_used,Index,as(Reference+Offset,address),"data"):-
    instruction_get_op(EA_def,_,Op_def),
    op_indirect(Op_def,"GS","NONE","NONE",_,0,_),
    def_used(EA_def,_,EA_used,Index),
    instruction_get_op(EA_used,Index,Op_used),
    op_indirect(Op_used,"NONE",_,_,_,Offset,_), Offset < 0,
    tls_segment(_,TlsEnd,Align),
    Reference = as(TlsEnd,number) + as(TlsEnd % max(Align,1),number).

symbolic_operand_attribute(EA,Index,"NtpOff"):-
    symbolic_operand(EA,Index,Dest,_),
    tls_segment(Start,End,_),
    Dest >= Start, Dest < End.


// GOT-relative data objects are sometimes used as references in loops.
// First a moved-label to the middle of a symbol:
//   e.g.   lea     esi, state@GOTOFF[ebx+1320]
// Followed by a boundary reference, relative to the data-object:
//   e.g.   lea     eax, state@GOTOFF[ebx-88]
// That is stored to the stack and compared in the loop:
//          mov     DWORD PTR -28[ebp], eax
//          sub     edi, 88
//          cmp     edi, DWORD PTR -28[ebp]
moved_label_class(EA_load,1,"got-data-object relative"),
moved_label(EA_load,1,Address,Base):-
    // GOT-relative stored in stack.
    got_relative_operand(EA_load,_,Dest),
    def_used(EA_load,Reg,EA_store,_),
    reg_stored_in_stack(EA_store,Reg,StackPos,StackFrame),

    // GOT-relative compared for jump.
    arch.frame_pointer(BaseReg),
    def_used(StackFrame,BaseReg,EA_cmp,_),
    arch.cmp_operation(Operation),
    instruction(EA_cmp,Size,_,Operation,Op1,_,0,0,_,_),
    op_indirect(Op1,"NONE",BaseReg,"NONE",_,StackPos,_),
    arch.jump(EA_cmp+Size),

    // Moved-value offset is the offset from the reference data object.
    def_used(EA_arith,_,EA_cmp,_),
    best_value_reg(EA_arith,_,EA_obj,_,_,"loop"),
    moved_label(EA_obj,1,_,Base),
    Offset=as(Dest-Base,number),
    Address=Base+as(Offset,address).
