//===- memory_access.dl -------------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019-2022 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
This module provides implements architecture-independent load and store
relations for x86.
*/

memory_access("LOAD",EA,1,2,DstReg,BaseReg,IndexReg,Mult,Offset):-
    instruction(EA,_,_,Operation,SrcOp,DestOp,0,0,_,_),
    move_operation(Operation),
    op_indirect_mapped(SrcOp,"NONE",BaseReg,IndexReg,Mult,Offset,_),
    op_regdirect_contains_reg(DestOp,DstReg).

// We consider the offset w.r.t the stack pointer BEFORE it has been updated.
memory_access("LOAD",EA,0,1,DstReg,StackReg,"NONE",0,0):-
    instruction(EA,_,_,"POP",Op,0,0,0,_,_),
    stack_pointer(StackReg),
    op_regdirect_contains_reg(Op,DstReg).

memory_access("STORE",EA,1,2,SrcReg,BaseReg,IndexReg,Mult,Offset):-
    instruction(EA,_,_,Operation,SrcOp,DestOp,0,0,_,_),
    move_operation(Operation),
    op_indirect_mapped(DestOp,"NONE",BaseReg,IndexReg,Mult,Offset,_),
    op_regdirect_contains_reg(SrcOp,SrcReg).

// We consider the offset w.r.t the stack pointer AFTER it has been updated.
memory_access("STORE",EA,1,0,SrcReg,StackReg,"NONE",0,0):-
    instruction(EA,_,_,"PUSH",Op,0,0,0,_,_),
    stack_pointer(StackReg),
    op_regdirect_contains_reg(Op,SrcReg).


store_immediate(EA,1,2,Immediate,BaseReg,IndexReg,Mult,Offset):-
    instruction(EA,_,_,Operation,SrcOp,DestOp,0,0,_,_),
    move_operation(Operation),
    op_indirect_mapped(DestOp,"NONE",BaseReg,IndexReg,Mult,Offset,_),
    op_immediate(SrcOp,Immediate).

// We consider the offset w.r.t the stack pointer AFTER it has been updated.
store_immediate(EA,1,2,Immediate,StackReg,"NONE",0,0):-
    instruction(EA,_,_,"PUSH",SrcOp,0,0,0,_,_),
    stack_pointer(StackReg),
    op_immediate(SrcOp,Immediate).
