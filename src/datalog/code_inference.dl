//===- code_inference.dl ----------------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
This module computes the valid instructions organized in blocks of
code.  It infers the facts 'code_in_block', 'block', and 'code'. In
addition it computes 'block_still_overlap' for debugging purposes.

The disassembly is done in two traversals:

  - A "must" backward traversal that propagates invalid instructions
  - A "may" forward traversal that tries to generate a
    superset of all possible code blocks (with the predicates possible_target
    and code_in_block_candidate). This traversal has a recursive component
    and a linear-sweep component.

Finally, we detect candidate blocks of code that overlap with each other (and
therefore are in conflict) and give them points according to several
heuristics. Then for each two conflicting blocks we keep the one with
more points.
*/


// These are final addresses where the code is located
// They are organized in blocks of sequential code
// The block identifier is the address of the first instruction in the block
.decl code_in_block(ea:address,DecodeMode:symbol,block:address)
.decl block(block:address, decode_mode: symbol)
.decl code(ea:address,DecodeMode:symbol)
.decl block_last_instruction(block:address,DecodeMode:symbol,EA:address)
.decl overlapping_instruction(EA:address,DecodeMode:symbol, EA2: address)
.decl data_in_code(Begin:address,End:address)
 // The postprocess refines the blocks by merging contiguous blocks
 // that are always executed together
#include "code_inference_postprocess.dl"

// cfg generates a control flow graph
#include "cfg.dl"

// we detect if there are still overlapping blocks,
// in that case the analysis has failed to resolve all conflicts
.decl block_still_overlap(ea:address,DecodeMode:symbol,ea2:address,DecodeMode2:symbol)
.output block_still_overlap

/////////////////////////////////////////////////////////////
// Basic facts to define fallthrough

.decl may_fallthrough(o:address,DecodeMode:symbol,d:address)

may_fallthrough(From,DecodeMode,To):-
    next(From,DecodeMode,To),
    !arch.return(From,DecodeMode),
    !unconditional_jump(From,DecodeMode),
    !halt(From,DecodeMode),
    !no_return_call(From,DecodeMode).

.decl must_fallthrough(o:address,DecodeMode:symbol,d:address)

must_fallthrough(From,DecodeMode,To):-
    may_fallthrough(From,DecodeMode,To),
    instruction_get_operation(From,DecodeMode,Operation),
    !arch.call_operation(Operation),
    !arch.syscall_operation(Operation),
    !arch.interrupt_operation(Operation),
    !instruction_has_loop_prefix(From,DecodeMode),
    !conditional_jump(From,DecodeMode).


//////////////////////////////////////////////////////////////
// This is a small refinement for discarding immediates as targets
// in some obvious cases. This is specially useful for PIE code where
// the address space starts from 0 (and therefore we have many more collisions).

.decl may_have_symbolic_immediate(src:address, DecodeMode:symbol,dest:address)

may_have_symbolic_immediate(Src,DecodeMode,Dest):-
    instruction_immediate_offset(Src,DecodeMode,Index,Offset),
    instruction_get_op(Src,DecodeMode,Index,Op),
    op_immediate(Op,Dest),
    (
        binary_type("DYN"),
        relocation(Src+Offset,_,_,_)
        ;
        binary_type("REL"),
        relocation(Src+Offset,_,_,_)
        ;
        binary_type("EXEC"),
        instruction_get_operation(Src,DecodeMode,Operation),
        !arch.logic_operation(Operation)
    ).


.decl invalid(EA:address,DecodeMode:symbol)
.output invalid
.decl invalid_edge(EA:address,DecodeMode:symbol,EA2:address)
.output invalid_edge

// if the decoder failed to decode any instruction at EA, EA is invalid
invalid(EA,DecodeMode):-
    invalid_op_code(EA,DecodeMode).

// if EA contains a direct call or jump to something that cannot be code, it is invalid
invalid_edge(EA,DecodeMode,Not_code),
invalid(EA,DecodeMode):-
    (
        must_fallthrough(EA,DecodeMode,Not_code), DecodeMode2 = DecodeMode;
        direct_jump(EA,DecodeMode,Not_code,DecodeMode2);
        direct_call(EA,DecodeMode,Not_code,DecodeMode2)
        //pc_relative_jump(EA,Not_code);
        //pc_relative_call(EA,Not_code)
    ),
    !instruction(Not_code,_,_,_,_,_,_,_,DecodeMode2).

// propagate the invalid instructions backwards until ret or jmp (encoded in must_fallthrough)
// propagate through direct jumps and calls
invalid_edge(From,DecodeMode,To),
invalid(From,DecodeMode):-
    invalid(To,DecodeModeTo),
    (
        must_fallthrough(From,DecodeMode,To), DecodeModeTo = DecodeMode;
        direct_jump(From,DecodeMode,To,DecodeModeTo);
        direct_call(From,DecodeMode,To,DecodeModeTo);
        pc_relative_jump(From,DecodeMode,To,DecodeModeTo);
        pc_relative_call(From,DecodeMode,To,DecodeModeTo)
    ).

.decl possible_ea(EA:address,DecodeMode:symbol)

possible_ea(EA,DecodeMode):-
    instruction(EA,_,_,_,_,_,_,_,DecodeMode),
    !invalid(EA,DecodeMode).

.decl basic_target(ea:address, DecodeMode:symbol)


basic_target(Val,"Normal"):-
    binary_isa("X64"),
    (
        relative_address(_,_,_,Val)
        ;
        binary_type("DYN"),
        address_in_data(EA,Val),
        relocation(EA,_,_,_)
        ;
        binary_type("REL"),
        address_in_data(EA,Val),
        relocation(EA,_,_,_)
        ;
        binary_type("EXEC"),
        address_in_data(_,Val)
        ;
        entry_point(Val)
        ;
        code_section(Name),
        section(Name,_,Val)
        ;
        defined_symbol(Val,_,_,_,_,_)
        ;
        // Exception information
        fde_addresses(Val,_)
        ;
        lsda_callsite_addresses(Val, _, _)
        ;
        lsda_callsite_addresses(_, _, Val)
    ).

.decl block_limit(EA:address)

block_limit(EA):-
    (
        op_immediate(_,EA);
        pc_relative_operand(_,_,_,EA);
        basic_target(EA,_);
        instruction_has_loop_prefix(EA,_);
        fde_addresses(_,EA)
    ).

// The targets are computed incrementally now as we traverse the code
// likely_ea and possible_target_from are mutually recursive

.decl possible_target_from(dest:address, DecodeMode:symbol ,src:address)

possible_target_from(Dest,DecodeModeDest,Src):-
    code_in_block_candidate(Src,DecodeMode,_),
    (
        may_have_symbolic_immediate(Src,DecodeMode,Dest),
        arch.existing_decode_modes(DecodeModeDest)
        ;
        direct_jump(Src,DecodeMode,Dest,DecodeModeDest);
        direct_call(Src,DecodeMode,Dest,DecodeModeDest);
        pc_relative_jump(Src,DecodeMode,Dest,DecodeModeDest);
        pc_relative_call(Src,DecodeMode,Dest,DecodeModeDest)

    ).

.decl possible_target(Target:address, DecodeMode:symbol)
.decl code_in_block_candidate(EA:address,DecodeMode:symbol,EA_block:address)
.output code_in_block_candidate

possible_target(EA,DecodeMode):-
    basic_target(EA,DecodeMode).

possible_target(EA,DecodeMode):-
    possible_target_from(EA,DecodeMode,_).

// start a new block given a possible target
code_in_block_candidate(EA,DecodeMode,EA):-
    possible_target(EA,DecodeMode),
    possible_ea(EA,DecodeMode).

// extend the block as long as we are sure to fallthrough and we have not
// reached a block limit
code_in_block_candidate(EA,DecodeMode,Start):-
    code_in_block_candidate(EA2,DecodeMode,Start),
    must_fallthrough(EA2,DecodeMode,EA),
    !block_limit(EA).


// if reached a block limit or an instruction that does not necessarily
// fallthrough continue exploring but start a new block
code_in_block_candidate(EA,DecodeMode,EA):-
    code_in_block_candidate(EA2,DecodeMode,Start),
    may_fallthrough(EA2,DecodeMode,EA),
    (
        !must_fallthrough(EA2,DecodeMode,EA);
        block_limit(EA)
    ),
    possible_ea(EA,DecodeMode).


// in case we cannot fallthrough, we skip any nops that we might find
// and generate a new possible_target once the nops finish
.decl after_end(EA:address,End:address)
// FIXME this might be underspecified
after_end(Next,End):-
    code_in_block_candidate(End,DecodeMode,_),
    next(End,DecodeMode,Next),
    !may_fallthrough(End,DecodeMode,Next).

after_end(Next,End):-
    after_end(EA,End),
    arch.is_nop(EA,DecodeMode),
    !block_limit(EA),
    next(EA,DecodeMode,Next).


possible_target(EA,"Normal"):-
    after_end(EA,_),
    (
        !arch.is_nop(EA,"Normal");
        block_limit(EA)
    ).

possible_target(EA,"Thumb"):-
    binary_isa("ARM"),
    after_end(EA,_),
    (
        !arch.is_nop(EA,"Thumb");
        block_limit(EA)
    ).
///////////////////////////////////////////////////////////////////////
// We are done with the recursive exploration
// Now we detect and resolve conflics within the different blocks

//First remove common tails
.decl common_tail(EA:address, DecodeMode:symbol)

common_tail(EA,DecodeMode):-
    code_in_block_candidate(EA, DecodeMode,Block),
    code_in_block_candidate(EA, DecodeMode,Block2),
    Block != Block2,
    next(EA_prev, DecodeMode,EA),
    code_in_block_candidate(EA_prev,DecodeMode,Block),
    !code_in_block_candidate(EA_prev,DecodeMode,Block2).

.decl code_in_block_candidate_refined(EA:address, DecodeMode:symbol,Block:address)

code_in_block_candidate_refined(Block,DecodeMode,Block):-
    code_in_block_candidate(Block,DecodeMode,Block).

code_in_block_candidate_refined(Block,DecodeMode,Block):-
    common_tail(Block,DecodeMode).

code_in_block_candidate_refined(EA_next,DecodeMode,Block):-
    code_in_block_candidate_refined(EA,DecodeMode,Block),
    next(EA,DecodeMode,EA_next),
    code_in_block_candidate(EA_next,DecodeMode,_),
    !code_in_block_candidate(EA_next,DecodeMode,EA_next),
    !common_tail(EA_next,DecodeMode).

.decl block_candidate_limit(EA:address,DecodeMode:symbol,Limit:address)

block_candidate_limit(Block,DecodeMode,Next):-
    code_in_block_candidate_refined(EA,DecodeMode,Block),
    next(EA,DecodeMode,Next),
    !code_in_block_candidate_refined(Next,DecodeMode,Block).

// auxiliary predicate to detect that there are several candidates that fallthrough into a third candidate block
.decl multiple_fallthrough_to(Block:address)
// This only makes sense within a single decode mode

multiple_fallthrough_to(Block):-
    may_fallthrough(EA,DecodeMode,Block),
    code_in_block_candidate_refined(EA,DecodeMode,BlockPred),
    may_fallthrough(EA2,DecodeMode,Block),
    code_in_block_candidate_refined(EA2,DecodeMode,BlockPred2),
    BlockPred != BlockPred2.

.decl instruction_range(EA:address,DecodeMode:symbol,EA_Instruction:address)

instruction_range(EA2+1,DecodeMode,EA2):-
    code_in_block_candidate_refined(EA2,DecodeMode,_),
    next(EA2,DecodeMode,End),
    EA2+1 < End.
instruction_range(EA+1,DecodeMode,EA2):-
    instruction_range(EA,DecodeMode,EA2),
    next(EA2,DecodeMode,End),
    EA+1 < End.

.decl block_overlap(ea:address,DecodeMode:symbol,ea2:address,DecodeMode2:symbol)
.output block_overlap

// an instruction of one block starts in the middle of another one from a different block
block_overlap(EA_block,DecodeMode,EA_block2,DecodeMode2):-
    code_in_block_candidate_refined(EA,DecodeMode,EA_block),
    instruction_range(EA_block2,DecodeMode,EA),
    code_in_block_candidate_refined(_,DecodeMode2,EA_block2).

//the blocks have instructions at the same address but with different decode modes
block_overlap(EA_block,DecodeMode,EA_block2,DecodeMode2):-
    code_in_block_candidate_refined(EA,DecodeMode,EA_block),
    code_in_block_candidate_refined(EA,DecodeMode2,EA_block2),
    DecodeMode != DecodeMode2.

.decl block_is_overlapping(block:address, DecodeMode: symbol)

block_is_overlapping(Block, DecodeMode):-
    (
        block_overlap(Block,DecodeMode,_,_);
        block_overlap(_,_,Block,DecodeMode)
    ).

block_points(Block,DecodeMode,0,-9,"overlap with relative-jump-table"),
block_is_overlapping(Block,DecodeMode):-
    block_candidate_limit(Block,DecodeMode,Limit),
    relative_address(Beg,Size,_,_),
    (
        Block <= Beg, Beg< Limit
        ;
        Beg < Block, Block< Beg+Size
    ).

//////////////////////////////////////////////////////////////////////
// We need to solve the block overlaps
// we do it with heuristics
///
.decl block_total_points(ea_block:address,DecodeMode:symbol, points:number)

block_total_points(Block,DecodeMode,Points):-
    block_is_overlapping(Block,DecodeMode),
    Points = sum X:{block_points(Block,DecodeMode,_,X,_)}.

.decl discarded_block(ea_block:address,DecodeMode:symbol)
.output discarded_block

discarded_block(Block,DecodeMode):-
    (
        block_overlap(Block,DecodeMode,Block2,DecodeMode2);
        block_overlap(Block2,DecodeMode2,Block,DecodeMode)
    ),
    // keep the block that has more points
    block_total_points(Block,DecodeMode,Points),
    block_total_points(Block2,DecodeMode2,Points2),
    Points < Points2.

discarded_block(Block,DecodeMode):-
    block_total_points(Block,DecodeMode,Points),
    Points < 0.

// propagate discarding blocks through direct jumps or calls
discarded_block(Block,DecodeMode):-
    discarded_block(Block2,DecodeMode2),
    (
        direct_jump(EA,DecodeMode,Block2,DecodeMode2);
        direct_call(EA,DecodeMode,Block2,DecodeMode2)
    ),
    code_in_block_candidate_refined(EA,DecodeMode,Block).

//////////////////////////////////////////////////////////////////////
//special cases

// two blocks where the first instruction is overlapping and only different because of a prefix
// we discard the second block but register the instruction without prefix as code
overlapping_instruction(Block2,DecodeMode,Block),
discarded_block(Block2,DecodeMode):-
    block_overlap(Block,DecodeMode,Block2,DecodeMode),
    // same number of points
    block_total_points(Block,DecodeMode,Points),
    block_total_points(Block2,DecodeMode,Points),
    // one has an extra prefix with respect to the other
    instruction(Block,Size,_,OpCode,Op1,Op2,Op3,Op4,DecodeMode),
    instruction(Block2,Size2,_,OpCode,Op1,Op2,Op3,Op4,DecodeMode),
    Block2 - Block = Size - Size2.


/**
 The heuristics give a variable number of points for things like number
 of predecessors, how the block is reached (through direct jump, fallthough, etc.)
 and if the block's address appears in the data section, whether it appears aligned
 or in a pointer array.
 The points are trasmitted transitively though overlapping blocks.
*/

.decl block_points(block:address,DecodeMode: symbol,predecessor:address,importance:number,why:symbol)
.output block_points


block_points(Block,DecodeMode,0,-6,"possible relative-jump-table start"):-
    block_is_overlapping(Block,DecodeMode),
    relative_address(Block,_,_,_),
    take_address(_,Block).

block_points(Block,DecodeMode,0,0,"basic point"):-
    block_is_overlapping(Block,DecodeMode),
    code_in_block_candidate_refined(_,DecodeMode,Block).

block_points(Block,DecodeMode,0,20,"start point"):-
    block_is_overlapping(Block,DecodeMode),
    entry_point(Block).

block_points(Block,DecodeMode,0,1,"code section start"):-
    block_is_overlapping(Block,DecodeMode),
    code_section(Section),
    section(Section,_,Block).

block_points(Block,DecodeMode,0,20,"function symbol"):-
    block_is_overlapping(Block,DecodeMode),
    function_symbol(Block,_).

block_points(Block,DecodeMode,0,1,"address in data"):-
    block_is_overlapping(Block,DecodeMode),
    address_in_data(Address,Block),
    Address % 8 != 0.

block_points(Block,DecodeMode,0,2,"aligned address in data"):-
    block_is_overlapping(Block,DecodeMode),
    address_in_data(Address,Block),
    Address % 8 = 0.

block_points(Block,DecodeMode,0,-1,"printable address"):-
    block_is_overlapping(Block,DecodeMode),
    address_in_data(Address,Block),
    data_byte(EA,Byte),(printable_char(Byte); Byte = 0),
    data_byte(EA+1,Byte1),(printable_char(Byte1); Byte1 = 0),
    data_byte(EA+2,Byte2),(printable_char(Byte2); Byte2 = 0),
    data_byte(EA+3,Byte3),(printable_char(Byte3); Byte3 = 0),
    data_byte(EA+4,Byte4),(printable_char(Byte4); Byte4 = 0),
    data_byte(EA+5,Byte5),(printable_char(Byte5); Byte5 = 0),
    data_byte(EA+6,Byte6),(printable_char(Byte6); Byte6 = 0),
    data_byte(EA+7,Byte7),(printable_char(Byte7); Byte7 = 0).

block_points(Block,DecodeMode,0,1,"address in data array"):-
    block_is_overlapping(Block,DecodeMode),
    address_in_data(Address,Block),
    Address % 8 = 0,
    (
        address_in_data(Address+8,_),
        address_in_data(Address+16,_)
        ;
        address_in_data(Address-8,_),
        address_in_data(Address-16,_)
        ;
        address_in_data(Address+8,_),
        address_in_data(Address-8,_)
    ).

block_points(Block,DecodeMode,BlockPred,6,"direct jump"):-
    block_is_overlapping(Block,DecodeMode),
    direct_jump(EA,DecodeModePred,Block,DecodeMode),
    code_in_block_candidate_refined(EA,DecodeModePred,BlockPred),
    BlockPred != Block,
    !block_is_overlapping(BlockPred,DecodeModePred).

block_points(Block,DecodeMode,BlockPred,6,"direct call"):-
    block_is_overlapping(Block,DecodeMode),
    direct_call(EA,DecodeModePred,Block,DecodeMode),
    code_in_block_candidate_refined(EA,DecodeModePred,BlockPred),
    BlockPred != Block,
    !block_is_overlapping(BlockPred,DecodeModePred).

block_points(Block,DecodeMode,BlockPred,2,"must-fallthrough"):-
    block_is_overlapping(Block,DecodeMode),
    must_fallthrough(EA,DecodeMode,Block),
    code_in_block_candidate_refined(EA,DecodeMode,BlockPred),
    !block_is_overlapping(BlockPred,DecodeMode).

block_points(Block,DecodeMode,BlockPred,7,"may-fallthrough"):-
    block_is_overlapping(Block,DecodeMode),
    may_fallthrough(EA,DecodeMode,Block),
    code_in_block_candidate_refined(EA,DecodeMode,BlockPred),
    !block_is_overlapping(BlockPred,DecodeMode).

// it fits but the previous block does not fallthrough
block_points(Block,DecodeMode,BlockPred,2,"fits"):-
    block_is_overlapping(Block,DecodeMode),
    after_end(Block,EA),
    code_in_block_candidate_refined(EA,DecodeMode,BlockPred),
    !block_is_overlapping(BlockPred,DecodeMode).

// its address appears somewhere
block_points(Block,DecodeMode,0,3,"appears"):-
    block_is_overlapping(Block,DecodeMode),
    possible_target_from(Block,DecodeMode,EA),
    !direct_jump(EA,DecodeModePred,Block,DecodeMode),
    !direct_call(EA,DecodeModePred,Block,DecodeMode),
    code_in_block_candidate_refined(EA,DecodeModePred,BlockPred),
    BlockPred != Block,
    !block_is_overlapping(BlockPred,DecodeModePred).

block_points(Block,DecodeMode,0,3,"relative jump table target"):-
    block_is_overlapping(Block,DecodeMode),
    relative_address(_,_,_,Block).

// give a point if it may fallthrough to another block
block_points(Block,DecodeMode,Block_succ,1,"fallthough_to"):-
    block_is_overlapping(Block,DecodeMode),
    code_in_block_candidate_refined(EA,DecodeMode,Block),
    may_fallthrough(EA,DecodeMode,Block_succ),
    code_in_block_candidate_refined(_,DecodeMode,Block_succ),
    !block_is_overlapping(Block_succ,DecodeMode).

// transitivity for fallthrough and direct jumps and calls
block_points(Block,DecodeMode,BlockPred2,Points,cat("fallthrough-transitive ",Why)):-
    block_is_overlapping(Block,DecodeMode),
    may_fallthrough(EA,DecodeMode,Block),
    !multiple_fallthrough_to(Block),
    code_in_block_candidate_refined(EA,DecodeMode,BlockPred),
    block_is_overlapping(BlockPred,DecodeMode),
    block_points(BlockPred,DecodeMode,BlockPred2,Points,Why),
    Points != 0.

block_points(Block,DecodeMode,BlockPred2,Points2,cat("fits-transitive",Why)):-
    block_is_overlapping(Block,DecodeMode),
    after_end(Block,EA),
    code_in_block_candidate_refined(EA,DecodeMode,BlockPred),
    block_is_overlapping(BlockPred,DecodeMode),
    block_points(BlockPred,_,BlockPred2,Points,Why),
    (
        Points > 2, Points2 = Points-2
        ;
        Points < -2, Points2 = Points+2
    ).

block_points(Block,DecodeMode,BlockPred2,Points2,cat("multiple-fallthrough-transitive ",Why)):-
    block_is_overlapping(Block,DecodeMode),
    may_fallthrough(EA,DecodeMode,Block),
    multiple_fallthrough_to(Block),
    code_in_block_candidate_refined(EA,DecodeMode,BlockPred),
    block_is_overlapping(BlockPred,DecodeMode),
    block_points(BlockPred,DecodeMode,BlockPred2,Points,Why),
    (
        Points > 1, Points2 = Points-1
        ;
        Points < -1, Points2 = Points+1
    ).

block_points(Block,DecodeMode,BlockPred2,Points,"jump-transitive"):-
    block_is_overlapping(Block,DecodeMode),
    direct_jump(EA,DecodeModePred,Block,DecodeMode),
    // do not count instructions that fallthrough and jump to the same place twice
    !may_fallthrough(EA,DecodeMode,Block),
    code_in_block_candidate_refined(EA,DecodeModePred,BlockPred),
    block_is_overlapping(BlockPred,DecodeModePred),
    block_points(BlockPred,DecodeModePred,BlockPred2,Points,_),
    Points != 0.

block_points(Block,DecodeMode,BlockPred2,Points,"call-transitive"):-
    block_is_overlapping(Block,DecodeMode),
    direct_call(EA,DecodeModePred,Block,DecodeMode),
    code_in_block_candidate_refined(EA,DecodeModePred,BlockPred),
    block_is_overlapping(BlockPred,DecodeModePred),
    block_points(BlockPred,DecodeMode,BlockPred2,Points,_),
    Points != 0.

block_points(Block,DecodeMode,0,1,"has well-defined jump"):-
    block_is_overlapping(Block,DecodeMode),
    direct_jump(EA,DecodeMode,BlockDest,DecodeModeDest),
    code_in_block_candidate_refined(EA,DecodeMode,Block),
    code_in_block_candidate_refined(_,DecodeModeDest,BlockDest),
    !block_is_overlapping(BlockDest,DecodeModeDest).

block_points(Block,DecodeMode,0,1,"has well-defined call"):-
    block_is_overlapping(Block,DecodeMode),
    direct_call(EA,DecodeMode,BlockDest,DecodeModeDest),
    code_in_block_candidate_refined(EA,DecodeMode,Block),
    code_in_block_candidate_refined(_,DecodeModeDest,BlockDest),
    !block_is_overlapping(BlockDest,DecodeModeDest).

block_points(Block,DecodeMode,0,1,"plt call"):-
    block_is_overlapping(Block,DecodeMode),
    direct_call(EA,DecodeMode,BlockDest,_),
    code_in_block_candidate_refined(EA,DecodeMode,Block),
    plt_entry(BlockDest,_).

block_points(Block,DecodeMode,0,10,"fde-entry"):-
    block_is_overlapping(Block,DecodeMode),
    fde_addresses(Block,_).

block_points(Block,DecodeMode,0,2,"padding nop block"):-
    block_is_overlapping(Block,DecodeMode),
    fde_addresses(_,Block),
    arch.is_nop(Block,DecodeMode).

block_points(Block,DecodeMode,0,-1,"overlap long nop"):-
    block_is_overlapping(Block,DecodeMode),
    instruction_range(Block,DecodeMode,EA_nop),
    arch.is_nop(EA_nop,_).

block_points(Block,DecodeMode,0,3,"exc-region"):-
    block_is_overlapping(Block,DecodeMode),
    lsda_callsite_addresses(Block, _, _).

block_points(Block,DecodeMode,0,10,"landing-pad"):-
    block_is_overlapping(Block,DecodeMode),
    lsda_callsite_addresses( _, _, Block).
////////////////////////////////////////////////////////////////////////////


code_in_block(EA,DecodeMode,Block):-
    code_in_block_candidate_refined(EA,DecodeMode,Block),
    !discarded_block(Block,DecodeMode).

code_in_block(EA,DecodeMode,Block):-
    overlapping_instruction(EA,DecodeMode,Block),
    code_in_block(Block,DecodeMode,Block).

block_last_instruction(Block,DecodeMode,EA):-
    code_in_block(EA,DecodeMode,Block),
    next(EA,DecodeMode,Next),
    !code_in_block(Next,DecodeMode,Block).

block(Block,DecodeMode):-
    code_in_block(_,DecodeMode,Block).

code(EA,DecodeMode):-
    code_in_block(EA,DecodeMode,_).

block_still_overlap(Block,DecodeMode,Block2,DecodeMode2):-
    block_overlap(Block,DecodeMode,Block2,DecodeMode2),
    !discarded_block(Block,DecodeMode),
    !discarded_block(Block2,DecodeMode2).


.decl data_in_code_propagate(Current:address,Initial:address,SectionLimit:address)

data_in_code_propagate(BlockEnd,BlockEnd,SectionLimit):-
    block_last_instruction(Block,DecodeMode,LastInsn),
    next(LastInsn,DecodeMode,BlockEnd),
    !block(BlockEnd,_),
    section(_,Size,AddrSection),
    SectionLimit = AddrSection+Size,
    AddrSection <= Block, BlockEnd < SectionLimit.

data_in_code_propagate(EA+1,Begin,SectionLimit):-
    data_in_code_propagate(EA,Begin,SectionLimit),
    !block(EA+1,_),
    EA+1 < SectionLimit.

data_in_code(Begin,End+1):-
    data_in_code_propagate(End,Begin,_),
    !data_in_code_propagate(End+1,Begin,_).


.decl next_block_in_section(Block:address,NextBlock:address)
// Blocks are either contiguous or separated by data segments
// This assumes there are no remaining overlapping blocks.

next_block_in_section(Block,NextBlock):-
    block_last_instruction(Block,LastInsn),
    next(LastInsn,NextBlock),
    block(NextBlock).

next_block_in_section(Block,NextBlock):-
    block_last_instruction(Block,LastInsn),
    next(LastInsn,DataBegin),
    data_in_code(DataBegin,NextBlock),
    block(NextBlock).
