//===- data_access_analysis.dl ----------------------------------*- C++ -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
 The data access analysis computes:

-'data_access_patterns' which represent that the address 'Address' is
 accessed with size 'Size' and multiplier 'Multiplier' from an
 instruction in 'FromWhere'.

-'preferred_data_access' the address is most likely accessed from the data access
  pattern located at ea_data_access.

data_access_patterns are computed by checking memory accesses and with
the help of the value analysis. Some special cases are checked
explicitly.

data_access_patterns give us very sparse information, but if an
address 'ea' is accessed with a multiplier, it is likely that
'ea'+multiplier, 'ea'+2*multiplier, etc are also accessed the same
way.  Unfortunately, we do not know until when. Therefore, we
propagate these accesses using the multiplier until we reach some
other access.

There are two refinements:

- Once the accesses have been propagated, we can have several data access patterns that
 have been propagated to the same location. We choose the one that is closest (thus the
 prefix 'preferred_'). The other propagated data accesses are discarded.

- Often we have data accesses that are related. For example accesses
to different fields of an array of structs. We say that those accesses
are 'paired'. In such cases, we can get better precision if we
propagate those accesses together. I.e. we stop the propagation as
soon as any of the paired accesses reaches another access.

Let us have two accesses to locations EA1 and EA2 with multipliers
Mult1 and Mult2.  We say these two accesses are paired if Mult1=Mult2
and EA1<= EA2<= EA1+Mult1.


*/

// auxiliary predicate to  compute the amount of read memory
.decl instruction_memory_access_size(EA:address,Op_index:operand_index,Size:number)

instruction_memory_access_size(EA,Index,SizeAccess):-
    code(EA),
    instruction_get_op(EA,Index,Op),
    instruction_get_operation(EA,Operation),
    (
        Operation="LEA",
        SizeAccess=0
     ;
        Operation!="LEA",
        op_indirect(Op,_,_,_,_,_,Size),
        SizeAccess=Size/8
    ).

//there is a data access at EA in the operand Op_index
.decl data_access(EA:address,Op_index:operand_index,Reg1:register,Reg2:register,Reg3:register,Mult:number,Offset:number,Size:number)

data_access(EA,Op_index,Reg1,Reg2,Reg3,Mult,Offset,Size):-
	code(EA),
	instruction_get_op(EA,Op_index,Op),
	op_indirect(Op,Reg1,Reg2,Reg3,Mult,Offset,_),
    instruction_memory_access_size(EA,Op_index,Size).

// the register Reg has the given value at instruction EA and operand Op_index
.decl value_reg_at_operand(EA:address,Op_index:operand_index,Reg:register,Mult:number,Offset:number,Type:symbol)

value_reg_at_operand(EA,Op_index,Reg,Mult,Offset_final,Type):-
	data_access(EA,Op_index,Reg1,Reg2,Reg3,_,_,Size),
	(Reg=Ref2; Reg= Reg3),
	Reg!="NONE",
	def_used(EA_def,Reg,EA,Op_index),
	best_value_reg(EA_def,Reg,Mult,Offset,Type),
	(
	Type!="incomplete",
	Offset_final=Offset
	;
	Type="incomplete",
	Offset_final=0
	).

// NONE registers have 0 value
value_reg_at_operand(EA,Op_index,Reg,0,0,"complete"):-
	data_access(EA,Op_index,Reg1,Reg2,Reg3,Mult,Offset,Size),
	(Reg=Ref2; Reg= Reg3),
	Reg="NONE".

.decl data_access_pattern(Address:address,Size:number,Multiplier:number,FromWhere:address)
.output data_access_pattern

.decl data_access_pattern_candidate(Address:address,Size:number,Multiplier:number,FromWhere:address)

.decl preferred_data_access(ea:address,ea_data_access:address)
.output preferred_data_access

//////////////////////////////////////////////////

    
//single access
data_access_pattern_candidate(Address,Size,0,EA):-
	data_access(EA,_,"NONE","NONE","NONE",_,Address,Size),
	data_section(Section),
	section(Section,SizeSect,Base),
	Address>=Base,
	Address<=Base+SizeSect.

// pc relative
data_access_pattern_candidate(Address,Size,0,EA):-
	code(EA),
	pc_relative_operand(EA,Op_index,Address),
    instruction_memory_access_size(EA,Op_index,Size),
	data_section(Section),
	section(Section,SizeSect,Base),
	Address>=Base,
	Address<=Base+SizeSect.

// directly computed
data_access_pattern_candidate(Address,Size,Mult*Mult2,EA):-
	data_access(EA,Op_index,"NONE",RegBase,RegMult,Mult,Offset1,Size),
	RegMult!="NONE",
	value_reg_at_operand(EA,Op_index,RegMult,Mult2,Offset2,_),
	value_reg_at_operand(EA,Op_index,RegBase,0,Offset3,"complete"),
	
	(Size<=Mult*Mult2; Mult*Mult2=0),
	Address=Offset1+(Offset2*Mult)+Offset3,
	data_section(Section),
	section(Section,SizeSect,Base),
	Address>=Base,
	Address<=Base+SizeSect.

// indirectly computed
data_access_pattern_candidate(Address,Size,Mult,EA):-
	data_access(EA,Op_index,"NONE",RegBase,RegMult,1,Offset1,Size),
	value_reg_at_operand(EA,Op_index,RegMult,0,Offset2,"complete"),
	value_reg_at_operand(EA,Op_index,RegBase,Mult,Offset3,_),

	(Size<=Mult; Mult=0),
	Address=Offset1+Offset2+Offset3,
	data_section(Section),
	section(Section,SizeSect,Base),
	Address>=Base,
	Address<=Base+SizeSect.
	
//repeated register
data_access_pattern_candidate(Address,Size,Mult+1,EA):-
	data_access(EA,Op_index,"NONE",Reg,Reg,Mult,Address,Size),
	value_reg_at_operand(EA,Op_index,Reg,1,0,_),
	Size<=Mult+1,

	data_section(Section),
	section(Section,SizeSect,Base),
	Address>=Base,
	Address<=Base+SizeSect.

// select the access pattern with highest multiplier
data_access_pattern(Address,Size,Multiplier,From):-
	data_access_pattern_candidate(Address,Size,Multiplier,From),
	data_byte(Address,_),
	MaxMult =max MaxMult : data_access_pattern_candidate(Address,Size,MaxMult,_),
	Multiplier>=MaxMult.


///////////////////////////////////////////////////////////////////////////////
//If threre are two nearby memory accesses with the same multiplier
// so their accesses are interleaved
// we consider they access the same data structure array
// so we propagate them together

// data_access_pattern at 'EA1' with size 'Size1' and multiplier 'Multiplier'
// is paired with data_access_pattern at 'EA2' with size 'Size2' and multiplier 'Multiplier'
.decl paired_data_access(EA1:address,Size1:number,Multiplier:number,
			 EA2:address,Size2:number,Diff:number)
			 
.output paired_data_access			 
paired_data_access(EA1,Size1,Multiplier,EA2,Size2,Diff):-
			 data_access_pattern(EA1,Size1,Multiplier,Ref1),
			 data_access_pattern(EA2,Size2,Multiplier,Ref2),
			 EA1<EA2,
			 EA1+Multiplier>=EA2+Size2,
			 Diff=EA2-EA1,
			 Ref1+200>=max(Ref1,Ref2),
			 Ref2+200>=max(Ref1,Ref2).


.decl next_paired_data_access(EA1:address,Size1:number,Multiplier:number,
			 EA2:address,Size2:number,Diff:number)
			 
next_paired_data_access(EA1,Size1,Multiplier,EA2,Size2,Diff2):-
	data_access_pattern(EA1,Size1,Multiplier,_),
	Min_diff=min Diff : paired_data_access(EA1,Size1,Multiplier,_,_,Diff),
	paired_data_access(EA1,Size1,Multiplier,EA2,Size2,Diff2),
	Min_diff>= Diff2.

.decl restart_paired_data_access(EA1:address,Size1:number,Multiplier:number,
			 EA2:address,Size2:number,Diff:number)
			 
restart_paired_data_access(EA1,Size1,Multiplier,EA2,Size2,Diff2):-
	data_access_pattern(EA1,Size1,Multiplier,_),
	!paired_data_access(_,_,Multiplier,EA1,Size1,_),
	
	Max_diff=max Diff : paired_data_access(EA1,Size1,Multiplier,_,_,Diff),
	paired_data_access(EA1,Size1,Multiplier,EA2,Size2,Diff2),
	Max_diff<= Diff2.
	

// the addresses that are covered by data_access_patterns
.decl taken_by_data_access_pattern(EA:address,Offset:number,DataAccessEA:address)

taken_by_data_access_pattern(EA,Size,EA):-
	data_access_pattern(EA,Size,_,_).
taken_by_data_access_pattern(EA+1,Rem-1,DataAccessEA):-
	taken_by_data_access_pattern(EA,Rem,DataAccessEA),
	Rem>1.


// group data according to labels
.decl individual_propagated_data_access(ea:address,ea_label:address,offset:number,multiplier:number)

individual_propagated_data_access(EA,EA,0,Mult):-
	data_access_pattern(EA,Size,Mult,_Ref),
	!paired_data_access(EA,Size,Mult,_,_,_),
	!paired_data_access(_,_,Mult,EA,Size,_).	
	
individual_propagated_data_access(EA+Multiplier,EA_label,EA+Multiplier-EA_label,Multiplier):-
	individual_propagated_data_access(EA,EA_label,_,Multiplier),
	data_byte(EA+Multiplier,_),
	//do not cross data access or labels
	(!symbolic_data_operand_candidate(_,_,EA+Multiplier)
	;
	//unless is a label generated by the same access
	symbolic_data_operand_candidate(Ref,_,EA+Multiplier),
	data_access_pattern(EA_label,_,Mult,Ref)
	),
	!address_in_data_refined(_,EA+Multiplier),
	(
            !taken_by_data_access_pattern(EA+Multiplier,_,_)
	;
            //it is taken by an access starting at the same point of
            //the original
            taken_by_data_access_pattern(EA+Multiplier,_,EA_label)
        ;
	   //is an access that matches the existing
	   data_access_pattern(EA+Multiplier,Size,Multiplier,Ref),
	   data_access_pattern(EA_label,Size,Multiplier,Reg)
	),
	pointer_size(Pt_size),
	address_array_range.no_overlap(EA,Pt_size).


// group data according to labels
.decl paired_propagated_data_access(ea:address,ea_label:address,offset:number,multiplier:number)

paired_propagated_data_access(EA,EA,0,Mult):-
	data_access_pattern(EA,Size,Mult,_),
	next_paired_data_access(EA,Size,Mult,_,_,_).
	
paired_propagated_data_access(EA,EA,0,Mult):-
	data_access_pattern(EA,Size,Mult,_),
	restart_paired_data_access(_,_,Mult,EA,Size,_).	
	
paired_propagated_data_access(EA+Multiplier,EA_label,EA+Multiplier-EA_label,Multiplier):-
	paired_propagated_data_access(EA,EA_label,_,Multiplier),
	data_byte(EA+Multiplier,_),
	//do not cross data access or labels	
	!symbolic_data_operand_candidate(_,_,EA+Multiplier),
	!address_in_data_refined(_,EA+Multiplier),
	(
            !taken_by_data_access_pattern(EA+Multiplier,_,_)
        ;
            //it is taken by an access starting at the same point of
            //the original
            taken_by_data_access_pattern(EA+Multiplier,_,EA_label)
	;
	   //is an access that matches the existing
	   data_access_pattern(EA+Multiplier,Size,Multiplier,Ref),
	   data_access_pattern(EA_label,Size,Multiplier,Reg)
	),
	pointer_size(Pt_size),
	address_array_range.no_overlap(EA,Pt_size),
//propagate paired accesses together
	(
	    next_paired_data_access(EA_paired,_,Multiplier,EA_label,_,Diff),
	    paired_propagated_data_access(EA+Multiplier-Diff,EA_paired,_,Multiplier)
	;
	    restart_paired_data_access(EA_label,_,Multiplier,EA_paired,_,Diff),
	    paired_propagated_data_access(EA+Diff,EA_paired,_,Multiplier)
	).
	
.decl propagated_data_access(ea:address,ea_label:address,distance:number,multiplier:number)
// count offset after the first propagation so patterns
// with higher multiplier prevail in the first propagation

propagated_data_access(EA,EA_label,Distance,Multiplier):-
	paired_propagated_data_access(EA,EA_label,_,Multiplier),
	Reference=EA_label+Multiplier,
	Distance=max(Reference-EA,EA-Reference).
propagated_data_access(EA,EA_label,Distance,Multiplier):-
	individual_propagated_data_access(EA,EA_label,_,Multiplier),
	Reference=EA_label+Multiplier,
	Distance=max(Reference-EA,EA-Reference).
	

.decl initial_preferred_data_access(EA:address,EA_label:address)

initial_preferred_data_access(EA,EA_label):-
	Min_Distance= min Distance:{propagated_data_access(EA,_,Distance,_)},
	propagated_data_access(EA,EA_label,Selected_distance,_),
	Min_Distance>=Selected_distance.

.decl discarded_propagated_access(EA:address,EA_label:address,Multiplier:number)

discarded_propagated_access(EA,EA_label2,Multiplier):-
	initial_preferred_data_access(EA,EA_label),
	propagated_data_access(EA,EA_label2,_,Multiplier),
	EA_label2!=EA_label.

discarded_propagated_access(EA+Multiplier,EA_label,Multiplier):-
	discarded_propagated_access(EA,EA_label,Multiplier),
	propagated_data_access(EA+Multiplier,EA_label,_,Multiplier).


preferred_data_access(EA,EA_label):-
	initial_preferred_data_access(EA,EA_label),
	!discarded_propagated_access(EA,EA_label,_).
	
