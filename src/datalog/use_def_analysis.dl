//===- use_def_analysis.dl --------------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
This modules computes def-uses chains for a subset of the registers (defined in track_register).
and only if those chains are related eventually to an access to memory.

-'def' computes definitions of registers at each of the locations.

-'def_used_for_address' compute the set of live uses backwards at each of the locations.
 However, it only computes uses starting at accesses to memory.

-'def_used' intersects the two previous facts to obtain def-use chains.

-'straight_line_def_used` is a weaker version of 'def_used' that can be used before
code inference.

The traversal of def_used_for_address is kept intra-procedural (not following call edges).
*/


// register is defined in this location
.decl def(EA:address,Reg:register)


/**
Instruction at address 'EA' uses register 'Reg' at operand index 'Index'.
*/
.decl used(EA:address,Reg:register,Index:operand_index)

//The 'reg' is defined in 'ea_def' and used in 'ea_used' in the operand with index 'index_used'
.decl def_used(ea_def:address,reg:register,ea_used:address,index_used:operand_index)
.output def_used

.decl def_used_intra(ea_def:address,reg:register,ea_used:address,index_used:operand_index)
.output def_used_intra

// a register is implicitly defined by being compared to a constant and then jumping
// this definition only takes place in between the jump and the target that implies equality
.decl flow_def(EA:address,Reg:register,EA_next:address,Value:number)
//////////////////////////////////////////////////////////////////////////////
//local underapproximation of control flow
.decl local_next(ea:address,ea_next:address)

local_next(EA,EA_next):-
    code_in_block(EA,Block),
    may_fallthrough(EA,EA_next),
    code_in_block(EA_next,Block).

.decl inter_procedural_jump(Src:address,Dest:address)

inter_procedural_jump(Src,Dest):-
    unconditional_jump(Src),
    direct_jump(Src,Dest),
    direct_call(OtherSrc,Dest),
    code(OtherSrc).

.decl block_next(ea:address,ea_next:address)


block_next(EA,Block2):-
    block_last_instruction(_,EA),
    may_fallthrough(EA,Block2),
    block(Block2).

block_next(EA,EA_next):-
    block_last_instruction(_,EA),
    !arch.delay_slot(_,EA),
    direct_jump(EA,EA_next),
    !inter_procedural_jump(EA,EA_next).

block_next(EA,EA_next):-
    block_last_instruction(_,EA),
    arch.delay_slot(JumpEA,EA),
    direct_jump(JumpEA,EA_next),
    !inter_procedural_jump(JumpEA,EA_next).

block_next(EA,Dest):-
    block_last_instruction(_,EA),
    jump_table_target(EA,Dest).

// Path created by exception handling.
block_next(EA,Block2):-
    lsda_callsite_addresses(Beg,End, Block2),
    block_last_instruction(_,EA),
    EA >= Beg, EA < End,
    block(Block2).

//////////////////////////////////////////////////////////////////////////////
// Main definitions

def(EA,Reg):-
    instruction_get_dest_op(EA,_,Op),
    op_regdirect_contains_reg(Op,Reg).

def(EA,Reg):-
    instruction_get_operation(EA,Operation),
    arch.call_operation(Operation),
    !arch.delay_slot(EA,_),
    arch.function_non_maintained_reg(Reg).

def(EA,Reg):-
    instruction_get_operation(Call,Operation),
    arch.call_operation(Operation),
    arch.delay_slot(Call,EA),
    arch.function_non_maintained_reg(Reg).

def(EA,Reg):-
    direct_call(EA,EA_pc_thunk),
    get_pc_thunk(EA_pc_thunk,Reg).

flow_def(EA_jump,Reg,EA_target,Immediate):-
    code(EA),
    cmp_immediate_to_reg(EA,Reg,_,Immediate),
    next(EA,EA_jump),
    direct_jump(EA_jump,EA_target),
    instruction_get_operation(EA_jump,Operation),
    arch.jump_equal_operation(Operation).

flow_def(EA_jump,Reg,EA_target,Immediate):-
    code(EA),
    cmp_immediate_to_reg(EA,Reg,_,Immediate),
    next(EA,EA_jump),
    direct_jump(EA_jump,_),
    next(EA_jump,EA_target),
    instruction_get_operation(EA_jump,Operation),
    arch.jump_unequal_operation(Operation).

.decl must_def(EA:address,Reg:register)

must_def(EA,Reg):-
    def(EA,Reg),
    instruction_get_operation(EA,Op),
    !arch.conditional_operation(Op).

.decl defined_in_block(EA:address,Reg:register)

defined_in_block(Block,Reg):-
    def(EA,Reg),
    code_in_block(EA,Block).

used(EA,Reg,Index):-
    instruction_get_src_op(EA,Index,Op),
    op_regdirect_contains_reg(Op,Reg),
    !is_xor_reset(EA).

used(EA,Reg,Index):-
    instruction_get_op(EA,Index,Op),
    op_indirect_contains_reg(Op,Reg).

.decl used_in_block(EA:address,Reg:register)

used_in_block(Block,Reg):-
    used(EA,Reg,_),
    code_in_block(EA,Block).

// the last address where a register was defined within the block
.decl block_last_def(EA:address,EA_def:address,Reg:register)



block_last_def(EA_next,EA,Reg):-
    def(EA,Reg),
    local_next(EA,EA_next).

block_last_def(EA_next,EA_def,Reg):-
    block_last_def(EA,EA_def,Reg),
    !must_def(EA,Reg),
    local_next(EA,EA_next).

// the last address where a register was defined at the basic block level.
.decl last_def(EA:address,EA_def:address,Reg:register)

last_def(Block,EA,Reg):-
    def(EA,Reg),
    block_next(EA,Block),
    !flow_def(EA,Reg,Block,_).

last_def(Block,EA_def,Reg):-
    block_last_def(Block_end,EA_def,Reg),
    !must_def(Block_end,Reg),
    block_next(Block_end,Block),
    !flow_def(Block_end,Reg,Block,_).

last_def(Block_next,EA_def,Reg):-
    last_def(Block,EA_def,Reg),
    !defined_in_block(Block,Reg),
    block_last_instruction(Block,Block_end),
    block_next(Block_end,Block_next),
    !flow_def(Block_end,Reg,Block,_).

last_def(Block_next,Block_end,Reg):-
    flow_def(Block_end,Reg,Block_next,_).


def_used_intra(EA_def,Reg,EA_used,Index):-
    used(EA_used,Reg,Index),
    block_last_def(EA_used,EA_def,Reg).


def_used_intra(EA_def,Reg,EA_used,Index):-
    last_def(Block,EA_def,Reg),
    code_in_block(EA_used,Block),
    !block_last_def(EA_used,_,Reg),
    used(EA_used,Reg,Index).

def_used(EA_def,Reg,EA_used,Index):-
    def_used_intra(EA_def,Reg,EA_used,Index).

def_used(EA_def,Reg,EA_used,Index_used):-
    def_used_return_val_reg(EA_def,_,Reg,EA_used,Index_used).

// reg: return-value register
// ea_used: use of the return-value register after a call
// ea_call: the corresponding call
// ea_def: def of the return-value register in the callee
.decl def_used_return_val_reg(ea_def:address,ea_call:address,reg:register,ea_used:address,index_used:operand_index)
.output def_used_return_val_reg

def_used_return_val_reg(EA_def,EA_call,Reg,EA_used,Index_used):-
    arch.return_val_reg(Reg),
    def_used_intra(EA_call,Reg,EA_used,Index_used),
    direct_call(EA_call,Callee),
    fde_addresses(Callee,End),
    arch.return(CalleeReturn),
    Callee <= CalleeReturn,
    CalleeReturn < End,
    code_in_block(CalleeReturn,CalleeReturnBlock),
    block_last_instruction(CalleeReturnBlock,CalleeReturnBlock_end),
    (
        block_last_def(CalleeReturnBlock_end,EA_def,Reg)
        ;
        !block_last_def(CalleeReturnBlock_end,_,Reg),
        last_def(CalleeReturnBlock,EA_def,Reg)
    ).

/**
 * The register 'Reg' used as an address at address 'EA'.
 */
.decl used_for_address(EA:address,Reg:register)
.output used_for_address

used_for_address(EA,Reg):-
    used(EA,Reg,Index),
    instruction_get_op(EA,Index,Op),
    op_indirect_contains_reg(Op,Reg).

used_for_address(EA,Reg):-
    reg_jump(EA,Reg),
    used(EA,Reg,_).

used_for_address(EA,Reg):-
    reg_call(EA,Reg),
    used(EA,Reg,_).

/**
 * The register 'Reg' as defined at address 'EA_def' is later used either as an address
 * or to compute an address.
 */
.decl def_used_for_address(EA_def:address,Reg:register)
.output def_used_for_address

def_used_for_address(EA_def,Reg):-
    def_used(EA_def,Reg,EA,_),
    used_for_address(EA,Reg).

def_used_for_address(EA_def,Reg):-
    def_used_for_address(EA_used,_),
    def_used(EA_def,Reg,EA_used,_).

def_used_for_address(EA,Reg):-
    arch.pc_relative_addr(EA,Reg,_).

// straight_line_def_used is a weaker version than def_used
// that can be used BEFORE code inference.


// the last address where a register was defined within the block
.decl straight_line_last_def(EA:address,EA_def:address,Reg:register)

straight_line_last_def(EA_next,EA,Reg):-
    def(EA,Reg),
    possible_ea(EA),
    must_fallthrough(EA,EA_next),
    possible_ea(EA_next).

straight_line_last_def(EA_next,EA_def,Reg):-
    straight_line_last_def(EA,EA_def,Reg),
    !must_def(EA,Reg),
    must_fallthrough(EA,EA_next),
    possible_ea(EA_next).

.decl straight_line_def_used(EA_def:address,Reg:register,EA_used:address,Index:unsigned)
.output straight_line_def_used

straight_line_def_used(EA_def,Reg,EA_used,Index):-
    used(EA_used,Reg,Index),
    straight_line_last_def(EA_used,EA_def,Reg).
